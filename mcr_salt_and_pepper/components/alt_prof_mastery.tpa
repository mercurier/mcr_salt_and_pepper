// Alternative proficiency mastery system
// Inspired by Scales of Balance and Tweak Anthology
// Maximum 4 stars for fighters (single or multiclass)
// True grand mastery

COPY ~mcr_salt_and_pepper/2da/alt_prof/^.+\.2da$~ ~override~

COPY_EXISTING ~weapprof.2da~  ~override~
  // update 5-star prof system to 4-star
  COUNT_2DA_COLS cols
  FOR (column = 4; column <= cols; column = column + 1) BEGIN
    FOR (row = 11; row < 31; row = row + 1) BEGIN
      READ_2DA_ENTRY row column 1 stars
      PATCH_IF (stars = 4) BEGIN
        SET_2DA_ENTRY_LATER ~alt_mastery~ row column ~3~
      END
      PATCH_IF (stars = 5) BEGIN
        SET_2DA_ENTRY_LATER ~alt_mastery~ row column ~4~
      END
    END
  END
  SET_2DA_ENTRIES_NOW ~alt_mastery~ 1

  // multiclass fighter full mastery
  FOR (column = 12; column < 21; column = column + 1) BEGIN
    FOR (row = 11; row < 31; row = row + 1) BEGIN
      READ_2DA_ENTRY row column 1 stars
      PATCH_IF (stars > 1) BEGIN
        SET_2DA_ENTRY_LATER ~multigrand~ row column ~4~
      END
    END
  END
  SET_2DA_ENTRIES_NOW ~multigrand~ 1
  BUT_ONLY
