// Alternative proficiency mastery system

// Patch APR
COPY_EXISTING ~wspatck.2da~ ~override~
	COUNT_2DA_COLS n_cols
	READ_2DA_ENTRY 5 1 n_cols 5_star_apr
	FOR (i=1; i<n_cols; i+=1) BEGIN
		PATCH_MATCH true_grandmastery_at WITH
		4 BEGIN
			// +0.5 apr for 3-star, netting 1 apr
			READ_2DA_ENTRY 3 i n_cols attacks
			SET_2DA_ENTRY_LATER ~wspatck2~ 3 i (attacks < 0)?(0 - attacks):(0 - attacks -1)
			// +1 apr for 4-star, netting 1.5 apr
			READ_2DA_ENTRY 4 i n_cols attacks
			SET_2DA_ENTRY_LATER ~wspatck2~ 4 i (attacks < 0)?(attacks -1):(attacks +1)
			SPRINT 4_star_desc @10325
			SPRINT 5_star_desc ~~
		END
		5 BEGIN
			// +0.5 apr for 4-star, netting 1 apr
			READ_2DA_ENTRY 4 i n_cols attacks
			new_att = (attacks < 0)?(0 - attacks):(0 - attacks -1)
			// PATCH_PRINT ~apr %attacks% -> %new_att%, 5s apr= %5_star_apr%~
			SET_2DA_ENTRY_LATER ~wspatck2~ 4 i (attacks < 0)?(0 - attacks):(0 - attacks -1)
			// set 1.5 apr for 5-star
			PATCH_IF 5_star_apr = 1 BEGIN  // if 5-star apr = 1, then +0.5 apr
				READ_2DA_ENTRY 5 i n_cols attacks
				SET_2DA_ENTRY_LATER ~wspatck2~ 5 i (attacks < 0)?(0 - attacks):(0 - attacks -1)
			END ELSE PATCH_IF 5_star_apr = 0-1 BEGIN  // if 5-star apr = 0.5, then +1 apr
				READ_2DA_ENTRY 5 i n_cols attacks
				SET_2DA_ENTRY_LATER ~wspatck2~ 5 i (attacks < 0)?(attacks -1):(attacks +1)
			END
			SPRINT 4_star_desc @10326
			SPRINT 5_star_desc @10327
		END DEFAULT 
			// +0.5 apr for 4-star, netting 1 apr
			READ_2DA_ENTRY 4 i n_cols attacks
			SET_2DA_ENTRY_LATER ~wspatck2~ 4 i (attacks < 0)?(0 - attacks):(0 - attacks -1)
			SPRINT 4_star_desc @10324
			SPRINT 5_star_desc ~~
		END
	END
	SET_2DA_ENTRIES_NOW ~wspatck2~ n_cols
	PRETTY_PRINT_2DA
BUT_ONLY


// Patch other bonus

COPY_EXISTING ~wspecial.2da~ ~override~
	COUNT_2DA_COLS n_cols
	SET_2DA_ENTRY_LATER ~wpspecial2~ 3 1 2
	SET_2DA_ENTRY_LATER ~wpspecial2~ 3 2 4
	SET_2DA_ENTRY_LATER ~wpspecial2~ 3 3 ~-1~
	SET_2DA_ENTRY_LATER ~wpspecial2~ 4 1 3
	SET_2DA_ENTRY_LATER ~wpspecial2~ 4 2 5
	SET_2DA_ENTRY_LATER ~wpspecial2~ 4 3 ~-3~
	
	SET_2DA_ENTRIES_NOW ~wpspecial2~ n_cols
	PRETTY_PRINT_2DA
BUT_ONLY

// Patch max # of stars for all classes 

COPY_EXISTING ~weapprof.2da~  ~override~
	// update 5-star prof system to 4-star
	COUNT_2DA_COLS cols
	FOR (column = 4; column < cols; ++column) BEGIN
		FOR (row = 11; row < 31; ++row) BEGIN // note: counting from line 0 at first line of file
			READ_2DA_ENTRY row column 1 stars
			PATCH_IF (stars = 4) BEGIN
				SET_2DA_ENTRY_LATER ~weapprof2~ row column 3
			END ELSE BEGIN
				PATCH_IF (stars = 5) BEGIN
					PATCH_IF (true_grandmastery_at != 5) OR
						((true_grandmastery_at = 5) AND (column != 32) AND (column != 36))
					BEGIN
						SET_2DA_ENTRY_LATER ~weapprof2~ row column 4
					END
				END
			END
		END
	END
	SET_2DA_ENTRIES_NOW ~weapprof2~ 1
	PRETTY_PRINT_2DA
BUT_ONLY

COPY_EXISTING ~PROFSMAX.2da~  ~override~
	COUNT_2DA_COLS n_cols
	COUNT_2DA_ROWS n_cols n_rows
	FOR (row = 0; row < n_rows; ++row) BEGIN
		SET_2DA_ENTRY_LATER ~PROFSMAX2~ row 3 2
		SET_2DA_ENTRY_LATER ~PROFSMAX2~ row 4 3
		// SET_2DA_ENTRY_LATER ~PROFSMAX2~ row 5 4
	END
	SET_2DA_ENTRIES_NOW ~PROFSMAX2~ n_cols
	PRETTY_PRINT_2DA
BUT_ONLY


// update bgee description

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
	// STRING_SET_EVALUATE  ~24219~ @10311 // 1 slot
	// STRING_SET_EVALUATE  ~24220~ @10312 // 2 slots
	STRING_SET_EVALUATE  ~24221~ @10323 // 3 slots
	STRING_SET_EVALUATE  ~24222~ ~%4_star_desc%~ // 4 slots
	STRING_SET_EVALUATE  ~24223~ ~%5_star_desc%~ // 5 slots
END

// update bg2ee and eet description

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
	// STRING_SET_EVALUATE  ~32116~ @10311 // 1 slot
	// STRING_SET_EVALUATE  ~32117~ @10312 // 2 slots
	STRING_SET_EVALUATE  ~32118~ @10323 // 3 slots
	STRING_SET_EVALUATE  ~32119~ ~%4_star_desc%~ // 4 slots
	STRING_SET_EVALUATE  ~32120~ ~%5_star_desc%~ // 5 slots
END
