

COPY_EXISTING_REGEXP GLOB ~.*\.itm$~ ~override~
	READ_SHORT 0x1c category
	READ_LONG 0x0c strref 
	GET_STRREF strref name2
	PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
		READ_BYTE 0x2d archer_unuse
		PATCH_MATCH category WITH
		15  // bows
		18  // slings
		24  // darts
		27  // crossbows
		BEGIN END// skip these items
		17 19 20 21 22 23 25 26 29 30 69 BEGIN  // melee weapons excluding dagger
			WRITE_BYTE 0x2d (archer_unuse BOR 0b10000000)
		END
		DEFAULT 
			READ_BYTE 0x2f kensai_unuse
			PATCH_IF (kensai_unuse BAND 0b00000100)>0 BEGIN
				WRITE_BYTE 0x2d (archer_unuse BOR 0b10000000)
			END
		END
	END
BUT_ONLY


COPY_EXISTING ~weapprof.2da~  ~override~
	// remove stars from all melee weapons and styles except for dagger
	patch_col = 36
	COUNT_2DA_COLS cols
	FOR (row = 11; row < 31; ++row) BEGIN // note: counting from first line of file as line 0
		READ_2DA_ENTRY row patch_col 1 stars
		PATCH_IF (stars = 1) BEGIN
			SET_2DA_ENTRY_LATER ~weapprof2~ row patch_col 0
		END
	END
	READ_2DA_ENTRY 29 4 1 mage_stars  // hybrid proficiency system compatibility
	READ_2DA_ENTRY 18 0 1 dagger_prof_name  // cdtweak weapon category combination compatibility
	PATCH_IF (~%dagger_prof_name%~ STRING_EQUAL_CASE ~DAGGER~) BEGIN
		SET_2DA_ENTRY_LATER ~weapprof2~ 18 patch_col mage_stars // dagger
	END ELSE BEGIN
		SET_2DA_ENTRY_LATER ~weapprof2~ 25 patch_col mage_stars // small_sword
	END
	
	SET_2DA_ENTRY_LATER ~weapprof2~ 31 patch_col 0 // styles
	SET_2DA_ENTRY_LATER ~weapprof2~ 32 patch_col 0
	SET_2DA_ENTRY_LATER ~weapprof2~ 33 patch_col 0
	SET_2DA_ENTRY_LATER ~weapprof2~ 34 patch_col 0
	// END
	SET_2DA_ENTRIES_NOW ~weapprof2~ 1
	PRETTY_PRINT_2DA
BUT_ONLY
